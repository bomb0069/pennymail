*** Settings ***
Resource            send_mail_library.txt
Suite Setup         Clean Up Mail Box
Test Setup          Open Sendmail Browser
Test Teardown       Close Browser

*** Test Cases ***
Send Mail To Recipient
	[Template]     Successfully Send Email To Recipient Workflow  
	#-------------------------------|----------------------|-------------------------------------|-------------------------|
	#       recipients              |          topic       |                  body               |   received mail count   |   
	#-------------------------------|----------------------|-------------------------------------|-------------------------|
	 a@gmail.com, b@gmail.com          New Gadget Coming       New Gadget arrive, Order Now !                  2
  	 a@gmail.com                       New Gadget Coming       ${EMPTY}                                        1
  	 a@gmail.com                       New Gadget Coming       <html><body>Gadget</body></html>                1
  	 
Send Mail To Recipient Without Topic
    I Compose Email Without Topic
    I Confirm To Send It When The Popup Displays
    Recipient Should Receive New Mail       ${EMPTY}     1
    
Send Mail To Recipient With Thai Content
    Send Email To Recipient               a@gmail.com             Mail Topic     สวัสดี
    Recipient Should Receive New Mail With Body                   Mail Topic     สวัสดี

*** Keywords ***
Clean Up Mail Box
    Open Mail Box
    Read All Mail
    
I Compose Email Without Topic
    Send Email To Recipient               a@gmail.com             ${EMPTY}              New Gadget arrive, Order Now !
    
I Compose Email With Body
    [Arguments]           ${body}
    Send Email To Recipient               a@gmail.com             Mail Topic     ${body}
    
I Confirm To Send It When The Popup Displays
    Click Button        okBtn