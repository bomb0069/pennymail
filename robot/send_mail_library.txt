*** Settings ***
Library             Selenium2Library
Library             check_mail.py
Resource            environment.txt

*** Variables ***
${SENDMAIL_URL}     		${PENNYMAIL_URL}/sendmail
${ID_CONFIRM_DIALOG_POPUP}	noTopicConfirmOverlay
${ID_CANCLE_BTN}			cancelBtn
${ID_CONFIRM_BTN}           okBtn

*** Keywords ***
Successfully Send Email To Recipient Workflow
	[Arguments]                          ${recipients}     ${topic}     ${body}    ${received_mail_count}
    Send Email To Recipient              ${recipients}     ${topic}     ${body}
    Everything Is OK
    Recipient Should Receive New Mail    ${topic}     ${received_mail_count}

Can't Send Email To Recipient Workflow
	[Arguments]                 ${recipients}     ${topic}     ${body}      ${message}
    Send Email To Recipient     ${recipients}     ${topic}     ${body}
    Penny Should See Message    ${message}
    
Popup Message Confirm Workflow
    [Arguments]                         ${recipients}     ${body}      ${id_button}  	${message}
    Send Email To Recipient             ${recipients}     ${EMPTY}     ${body}
    Wait Until Page Contains		    ${message}
    Click Button                        ${id_button}
    Element Should Not Be Visible       ${ID_CONFIRM_DIALOG_POPUP}
    
Send Email To Recipient
	[Arguments]                 ${recipients}     ${topic}     ${body}
	Go To                       ${SENDMAIL_URL}
	Fill in Recipients          ${recipients}
    Fill in Topic               ${topic}
    Fill in Message             ${body}
    Click Button                send

Open Sendmail Browser
	Open Browser                  ${SENDMAIL_URL}
#	Set Selenium Speed            0.5
    Wait Until Page Contains      RECIPIENTS

Fill in Recipients
	[Arguments]     ${recipients}
    Input Text      recipients      ${recipients}

Fill in Topic
	[Arguments]     ${topic}
    Input Text      topic           ${topic}

Fill in Message
	[Arguments]     ${message}
    Input Text      message         ${message}

Penny Should See Message
    [Arguments]     ${message}
    Page Should Contain             ${message}
    
Recipient Should Receive New Mail
    [Arguments]           ${topic}     ${mail_count}
    Open Mail Box
    Should Receive New Mail      ${topic}     ${mail_count}
    Close Mail Box

Penny Should Not See Message
	[Arguments]                     ${message}
    Page Should Not Contain         ${message}
    
Everything Is OK
    Penny Should Not See Message    à¹�à¸ªà¸£à¸”à¸” e Penny à¹‚à¸‡à¹ˆ
